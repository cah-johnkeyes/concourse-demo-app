buildscript {
	ext {
		kotlinVersion = '1.0.3'
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
	}
}

apply plugin: 'kotlin'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'

group='com.cardinalhealth'
jar {
	baseName = 'anagramalyzer'
	version = "1.0.${new Date().time}"
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

ext {
	repoUsername = System.env.REPO_USERNAME ?: "local"
	repoPassword = System.env.REPO_PASSWORD ?: "local"
	repoUrl = System.env.REPO_URL ?: "local"
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'com.cardinalhealth'
			artifactId jar.baseName
		}
	}
	repositories {
		maven {
			credentials {
				username "${repoUsername}"
				password "${repoPassword}"
			}
			// change to point to your repo, e.g. http://my.org/repo
			url "${repoUrl}"
		}
	}
}

task('writeVersionFile') << {
	def file = new File("${buildDir}/VERSION")
	file << jar.version

}
